ORG 0x3D3

N:      WORD    0x609   ; Адрес первой ячейки строки
;N:      WORD    $S      ; Адрес первой ячейки строки
LEN:    WORD    ?       ; Количество символов

START:  CLA             ; Очистка аккумулятора
        LD      (N)+    ; Загрузка количества символов
        BEQ     STOP    ; Количество символов == 0? Стоп
        BMI     STOP    ; Количество символов < 0? Стоп
        ST      LEN     ; Иначе - сохранить в LEN
S1:     IN      7       ; Ввод SR
        AND     #0x40   ; Бит 6 SR == 0 («Готов» нажата?)
        BEQ     S1      ; Нет - "Спин-луп"
        LD      (N)     ; Загрузка слова по адресу из N
        SWAB            ; Получение первого символа
        OUT     6       ; Вывод первого символа в DR

        LOOP    LEN     ; Остались ли еще символы?
        JUMP    S2      ; Да - S2
        JUMP    STOP    ; Нет - стоп
S2:     IN      7       ; Ввод SR
        AND     #0x40   ; Бит 6 SR == 0 («Готов» нажата?)
        BEQ     S2      ; Нет - "Спин-луп"
        LD      (N)+    ; Загрузка слова по адресу из N
        OUT     6       ; Вывод второго символа в DR

        LOOP    LEN     ; Остались ли еще символы?
        JUMP    S1      ; Да - S1
STOP:   HLT             ; Нет - стоп


S:      WORD    0x0005
VALUES: WORD    0xEEC1, 0xDAC9, 0xCD00